plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.jaredsburrows.license'
}

android {
    namespace 'com.github.cvzi.wallpaperexport'
    compileSdk 34

    defaultConfig {
        applicationId "com.github.cvzi.wallpaperexport"
        minSdk 28
        targetSdk 34
        versionCode 6
        versionName "1.1.1"
    }

    if (project.hasProperty("keystore")) {
        signingConfigs {
            release {
                storeFile rootProject.file(project.property("keystore").toString())
                storePassword project.property("keystorepassword").toString()
                keyAlias project.property("keystorealias").toString()
                keyPassword project.property("keystorekeypassword").toString()
                enableV1Signing = true
                enableV2Signing = true
                enableV3Signing = true
            }
        }
    } else {
        signingConfigs {
            release {
                storeFile rootProject.file('mykey.jks')
                storePassword 'password'
                keyAlias 'key0'
                keyPassword 'password'
                enableV1Signing = true
                enableV2Signing = true
                enableV3Signing = true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = 17
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.1"
}

licenseReport {
    // Run via `gradlew licenseReleaseReport`
    generateCsvReport = false
    generateHtmlReport = true
    generateJsonReport = false
    copyHtmlReportToAssets = true
}
